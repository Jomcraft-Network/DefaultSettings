plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
	id 'net.darkhax.curseforgegradle' version '1.0.11'
}

version = "1.19.3-2.8.7"

def fileVersion = "2.8.7"
group = "net.jomcraft.defaultsettings"

archivesBaseName = "DefaultSettings"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: 'official', version: '1.19.3'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                defaultsettings {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                defaultsettings {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'defaultsettings', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                defaultsettings {
                    source sourceSets.main
                }
            }
        }
    }
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.3-44.0.0'
    implementation fg.deobf("curse.maven:jcp-659192:3951271")
}

def forge_version = "41.0.0"

processResources {

	duplicatesStrategy = 'include'

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': fileVersion, 'forge_version': forge_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

jar {
	manifest {
		attributes 'FMLAT': 'defaultsettings_at.cfg'
		attributes([
			"Specification-Title": "DefaultSettings",
			"Specification-Vendor": "Jomcraft Network",
			"Specification-Version": fileVersion,
			"Implementation-Title": "DefaultSettings",
			"Implementation-Version": fileVersion,
			"Implementation-Vendor" : "Jomcraft Network",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}

}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
}

artifacts {
	archives jar
	archives sourcesJar
	archives deobfJar
}

jar.finalizedBy('reobfJar')

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	if(System.getenv("CURSE_API") != null && !System.getenv("CURSE_API").equals("")) {
		disableVersionDetection()

		apiToken = System.getenv("CURSE_API")

		def mainFile = upload(318012, jar)
		mainFile.releaseType = 'release'
		mainFile.changelogType = 'html'
		mainFile.changelog = file('changelog.html')
		mainFile.addRequirement('jcplugin')
		mainFile.displayName = "$archivesBaseName-$version"
		mainFile.addModLoader('Forge')
		mainFile.addJavaVersion('Java 8')
		mainFile.addJavaVersion('Java 16')
		mainFile.addJavaVersion('Java 17')
		mainFile.addGameVersion('1.19.3')

		def sourcesFile = mainFile.withAdditionalFile(sourcesJar)
		sourcesFile.addRequirement('jcplugin')
		sourcesFile.changelog = file('changelog.html')
		sourcesFile.displayName = "$archivesBaseName-$version-sources"

		def deobfFile = mainFile.withAdditionalFile(deobfJar)
		deobfFile.addRequirement('jcplugin')
		deobfFile.changelog = file('changelog.html')
		deobfFile.displayName = "$archivesBaseName-$version-deobf"
	}
}