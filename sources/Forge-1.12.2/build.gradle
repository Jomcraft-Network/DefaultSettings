buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'net.darkhax.curseforgegradle' version '1.0.11'
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'

archivesBaseName = "${rootProject.name}"

def file_version = version;

ext.replace_version(version)

version = "1.12.2-${version}-Forge"

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        client {
            workingDirectory file('./run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            ideaModule "DefaultSettings.Forge-1.12.2.main"
            property 'forge.logging.console.level', 'debug'
            mods {
                "${rootProject.name}" {
                    source sourceSets.main
                    //source project(":Core").sourceSets.main
                }
            }
        }

        server {
            workingDirectory file('./run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            ideaModule "DefaultSettings.Forge-1.12.2.main"
            property 'forge.logging.console.level', 'debug'
            mods {
                "${rootProject.name}" {
                    source sourceSets.main
                    //source project(":Core").sourceSets.main
                }
            }
        }
    }
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
    implementation fg.deobf("curse.maven:jcp-659192:4407625")
    //implementation project(":Core")
}

processResources {
    duplicatesStrategy = 'include'

    inputs.property "version", file_version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': file_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.withType(JavaCompile) {
    //source(project(":Core").sourceSets.main.allSource)
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

jar {
    manifest {
        attributes 'FMLAT': 'accesstransformer.cfg'
    }
}

artifacts {
    archives jar
    archives sourcesJar
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    if (System.getenv("CURSE_API") != null && !System.getenv("CURSE_API").equals("")) {
        disableVersionDetection()
        apiToken = System.getenv("CURSE_API")

        def mainFile = upload(318012, jar)
        mainFile.releaseType = 'alpha'
        mainFile.displayName = "$archivesBaseName-$version"
        mainFile.changelogType = 'html'
        mainFile.changelog = file('../../changelog.html')
        mainFile.addModLoader('Forge')
        mainFile.addJavaVersion('Java 8')
        mainFile.addJavaVersion('Java 16')
        mainFile.addJavaVersion('Java 17')
        mainFile.addGameVersion('1.19.3')
        mainFile.addGameVersion('1.19.2')
        mainFile.addGameVersion('1.18.2')
        mainFile.addGameVersion('1.17.1')
        mainFile.addGameVersion('1.16.5')

        def sourcesFile = mainFile.withAdditionalFile(sourcesJar)
        sourcesFile.changelog = file('../../changelog.html')
        sourcesFile.displayName = "$archivesBaseName-$version-sources"
    }
}

task("copyRelease") {
    dependsOn "build"

    doFirst {
        println "Gathering builds"
        copy {
            def libDir = project.projectDir.toPath().resolve("build/libs")
            from(libDir) {
                include "*.jar"
                exclude "*-dev.jar", "*-sources.jar"
            }
            into "../../build/libs/"
            duplicatesStrategy DuplicatesStrategy.INCLUDE
        }
    }
}