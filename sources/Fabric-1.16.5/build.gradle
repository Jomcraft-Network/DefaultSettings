plugins {
	id "fabric-loom" version "1.0-SNAPSHOT"
	id 'net.darkhax.curseforgegradle' version '1.0.11'
}

archivesBaseName = "${rootProject.name}"

def file_version = version;

version = "1.16.x-1.18.x-${version}-Fabric"

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
	minecraft "com.mojang:minecraft:1.16.5"
	mappings loom.officialMojangMappings()

	modImplementation "net.fabricmc:fabric-loader:0.11.3"

	modImplementation "net.fabricmc.fabric-api:fabric-api:0.34.2+1.16"
	
	//implementation fg.deobf("curse.maven:jcp-659192:4404202")
	modImplementation "curse.maven:jcp-659192:4416087"
	//implementation project(":Core")
}

tasks.withType(JavaCompile) {
	//source(project(":Core").sourceSets.main.allSource)
}

loom {

	accessWidenerPath = file("src/main/resources/defaultsettings.accesswidener")

	runs {
		client {
			client()
			setConfigName("Fabric 1.16.5 Client")
			ideConfigGenerated(true)
			runDir("./run")
		}
		server {
			server()
			setConfigName("Fabric 1.16.5 Server")
			ideConfigGenerated(true)
			runDir("./run")
		}
	}

	//mixin {
	//	defaultRefmapName = "${project.rootProject.name}.refmap.json"
	//}
}

//java {
//	withSourcesJar()
//}

processResources {
	inputs.property "version", file_version
	inputs.property "JCPluginVersion", rootProject.JCPluginVersion

	filesMatching(["**/fabric.mod.json", "**/mods.toml"]) {
		expand "version": file_version, "JCPluginVersion": project.JCPluginVersion
	}
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives sourcesJar
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	dependsOn "build"
	if (System.getenv("CURSE_API") != null && !System.getenv("CURSE_API").equals("")) {
		disableVersionDetection()
		apiToken = System.getenv("CURSE_API")

		def mainFile = upload(451860, remapJar.archiveFile)
		mainFile.releaseType = 'beta'
		mainFile.displayName = "$archivesBaseName-$version"
		mainFile.changelogType = 'html'
		mainFile.changelog = file('../../changelog.html')
		mainFile.addRequirement('jcplugin')
		mainFile.addModLoader('Fabric')
		mainFile.addJavaVersion('Java 8')
		mainFile.addJavaVersion('Java 16')
		mainFile.addJavaVersion('Java 17')
		mainFile.addGameVersion('1.18.2')
		mainFile.addGameVersion('1.17.1')
		mainFile.addGameVersion('1.16.5')

		def sourcesFile = mainFile.withAdditionalFile(sourcesJar)
		sourcesFile.addRequirement('jcplugin')
		sourcesFile.changelog = file('../../changelog.html')
		sourcesFile.displayName = "$archivesBaseName-$version-sources"
	}
}

task("copyRelease") {
	dependsOn "build"

	doFirst {
		println "Gathering builds"
		copy {
			def libDir = project.projectDir.toPath().resolve("build/libs")
			from(libDir) {
				include "*.jar"
				exclude "*-dev.jar", "*-sources.jar"
			}
			into "../../build/libs/"
			duplicatesStrategy DuplicatesStrategy.INCLUDE
		}
	}
}